plugins {
    id "com.jfrog.bintray" version "1.2"
	id "com.jfrog.artifactory" version "3.1.1"
	id "com.gradle.plugin-publish" version "0.9.1"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'net.foragerr.jmeter'
archivesBaseName = 'jmeter-gradle-plugin'

//--- versioning information ---
ext.thisPluginVersion="0.5.6"
ext.jmVersion = "2.13"
ext.jmPluginVersion = "1.3.0" //jmeter-plugin version
version = "$thisPluginVersion-$jmVersion"

//compileGroovy {
//    sourceCompatibility = '1.6'
//    targetCompatibility = '1.6'
//}

def bootClasspathStr = "${System.env.'JAVA_HOME'}/jre/lib/rt.jar"
project.tasks.withType(AbstractCompile, { AbstractCompile ac ->
    ac.options.bootClasspath = bootClasspathStr // options is always there but not defined on AbstractCompile so going to hit it anyway
})

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

configurations {
	//workaround for jmeter defect 57555 (https://bz.apache.org/bugzilla/show_bug.cgi?id=57555)
	if (jmVersion == '2.12'){
		compile.exclude group: 'excalibur-fortress', module: 'excalibur-fortress-container-api'
		compile.exclude group: 'excalibur-fortress', module: 'excalibur-fortress-meta'
 	
	}
	//workaround for jmeter defect 57748 (https://bz.apache.org/bugzilla/show_bug.cgi?id=57748)
	if (jmVersion == '2.13'){
		compile.exclude group: 'commons-math3', module: 'commons-math3'
		compile.exclude group: 'commons-pool2', module: 'commons-pool2'
	}
}

dependencies {
	
	compile gradleApi(),
		localGroovy(),
        "kg.apc:jmeter-plugins:${jmPluginVersion}",
        "kg.apc:jmeter-plugins-standard:${jmPluginVersion}",
        "kg.apc:jmeter-plugins-extras:${jmPluginVersion}",
        "kg.apc:jmeter-plugins-common:${jmPluginVersion}",
		"org.apache.ant:ant:1.8.4",
		"org.beanshell:bsh-core:2.0b4",
		"org.apache.jmeter:ApacheJMeter_http:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_junit:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_java:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_jdbc:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_tcp:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_mail:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_ldap:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_ftp:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_functions:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_monitors:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_jms:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_components:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_config:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_core:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_junit:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_components:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_config:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_junit-test:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_mongodb:${jmVersion}",
		"org.apache.jmeter:ApacheJMeter_native:${jmVersion}",
		"org.apache.commons:commons-math3:3.4.1",
		"org.apache.commons:commons-pool2:2.3"

	testCompile "junit:junit:4.11"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

processResources.doLast {
    ant.replace(
            file: "${project.buildDir}/resources/main/jmeter-plugin.properties",
            token: "@jmeter.version@",
            value: "$jmVersion"
    )
    ant.replace(
            file: "${project.buildDir}/resources/main/jmeter-plugin.properties",
            token: "@plugin.version@",
            value: "$jmPluginVersion"
    )
}

//--- mvn-publish ---
def pomConfig = {
	scm {
		url 'scm:git@github.com:jmeter-gradle-plugin/jmeter-gradle-plugin.git'
		connection 'scm:git@github.com:jmeter-gradle-plugin/jmeter-gradle-plugin.git'
		developerConnection 'scm:git@github.com:jmeter-gradle-plugin/jmeter-gradle-plugin.git'
	}
	licenses {
		license {
			name 'The Apache Software License, Version 2.0'
			url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'foragerr'
			name 'RaGe'
			url 'https://github.com/jmeter-gradle-plugin/jmeter-gradle-plugin'
			email 'foragerr@gmail.com'
			roles { role 'Developer' }
		}
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			//since maven-publish refuses to propogate exclusions to pom, DIY. :(
			from(components.java)
			def dependencies =  project.configurations[JavaPlugin.COMPILE_CONFIGURATION_NAME].getAllDependencies()
			def xmlExclusionList = project.configurations[JavaPlugin.COMPILE_CONFIGURATION_NAME].getExcludeRules()
			pom.withXml{
				asNode().dependencies.dependency.each{ xmlDep ->
					//delete scope tag
					def xmlScope = xmlDep.get("scope")	
					if (xmlScope) xmlDep.remove(xmlScope)
					//insert exclusions
					def xmlExclusions = xmlDep.exclusions[0]
					if (!xmlExclusions) xmlExclusions = xmlDep.appendNode('exclusions')
					xmlExclusionList.each{ ExcludeRule exRule ->
						def xmlExclusion = xmlExclusions.appendNode('exclusion')
						xmlExclusion.appendNode('groupId', exRule.group)
						xmlExclusion.appendNode('artifactId', exRule.module)
					}	
				}
				
			}
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'A gradle plugin for running jmeter tests')
				root.children().last() + pomConfig
			}
			artifact sourcesJar
			artifact javadocJar
        }
    }
}

//--- bintray publishing ---
bintray {
	user=System.getenv('bintray_user')
	key=System.getenv('bintray_api_key')
	
    publications = ['mavenJava'] //When uploading Maven-based publication files
    publish = true //If version should be auto published after an upload
    pkg {
        repo = "jmeter"
        name = "$archivesBaseName"
        userOrg = "$archivesBaseName" 
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        websiteUrl = 'http://foragerr.net/jmeter'
        issueTrackerUrl = 'https://github.com/jmeter-gradle-plugin/jmeter-gradle-plugin/issues'
        vcsUrl = 'https://github.com/jmeter-gradle-plugin/jmeter-gradle-plugin.git'

        version {
            name = "$version" //Bintray logical version name
            gpg { sign = true }
            
			//Optional configuration for Maven Central sync of the version
            mavenCentralSync {
                sync = false //true by default
                user = 'userToken' //OSS user token
                password = 'paasword' //OSS user password
                close = '0' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }            
        }
    }
}

//--- oss.jfrog artifactory publishing ---


//--- gradle publishing ---
pluginBundle {
	website = 'http://foragerr.net/jmeter'
	vcsUrl = 'https://github.com/jmeter-gradle-plugin/jmeter-gradle-plugin.git'
	description = 'A gradle plugin for running jmeter tests'
	tags = ['jmeter', 'performance']
  
	plugins {
	  greetingsPlugin {
		id = 'net.foragerr.jmeter'
		displayName = 'JMeter Gradle Plugin'
	  }
	}
  }